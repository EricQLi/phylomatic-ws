#!/usr/bin/gawk -f

## Webservice for phylomatic 

## This program is free software; you can redistribute it and/or
## modify it under the terms of the BSD 2-Clause License:
## http://www.opensource.org/licenses/bsd-license.php
## Copyright (c) 2012, Campbell Webb <cwebb@oeb.harvard.edu>

# (requires gawk4)

@include "lib/fyt2new.awk"
@include "lib/cdao2fyt.awk"
@include "lib/readfyt.awk"
@include "lib/utils.awk"
@include "lib/new2fyt.awk"

BEGIN{

  ORS = "";
  PS = PROCINFO["pid"] ;

  # set execs
  findexecs();

  # clean old files (older than one minute)
  system("rm -f `find run -type f -cmin +1 -not -name .gitignore -print` \
          >& /dev/null");

  # GET or POST?
  # test with curl -d "a=b" [-G] <URL>
  RS = "\x04";
  if (ENVIRON["QUERY_STRING"])
	{
	  IN = ENVIRON["QUERY_STRING"] ;
	}
  else getline IN;

  # parse input
  split( IN, query, "&" );
  for (i in query)
	{
	  split(query[i],tmpquery,"=");
	  # test for size of tree
	  if (length(tmpquery[2]) > 10000) 
		{ error("Trees over 10kB must be passed by URI") }  
	  f[tmpquery[1]] = decode(tmpquery[2]);
	}

  # Check for switches:

  if ((!f["intree"]) && (!f["intreeuri"]) && (!f["storedtree"])) 
	{ error("either intree or intreeuri or storedtree must be given") }
  if (length( substr(f["intree"],1,1) substr(f["intreeuri"],1,1) \
			  substr(f["storedtree"],1,1)) > 1) 
	{ error("either intree OR intreeuri OR storedtree must be given") }
  if ((f["method"] != "phylomatic") && (f["method"] != "convert"))
	{ error("method of phylomatic or convert must be given") }
  if ((f["method"] == "phylomatic") && (!f["taxa"]))
	{ error("if method=phylomatic, taxa must be given") }
  if ((f["informat"] != "newick") && (f["informat"] != "nexml") && \
	  (f["informat"] != "cdaordf"))
	{ error("informat must be either newick, nexml or cdaordf") }

  # BEGIN controller

  # MEGATREE IN:

  if ((f["intree"]) &&  (f["informat"] == "nexml"))
	{
	  cmd = XGAWK " -f lib/nexml2fyt.awk" ;
	  print f["intree"] |& cmd ;
	  close(cmd, "to");
	  RS = "\x04"; 
	  cmd |& getline results ;
	  close(cmd);
	  readfyt(results);
	}

  else if ((f["intreeuri"]) &&  (f["informat"] == "nexml"))
	{
	  # for testing:
	  # curl --data-urlencode intree@tmp/nexmlconvert.post -d method=convert \
	  #   -d informat=nexml http://localhost/~cam/phylomatic-ws/pmws
      # curl -d intreeuri=http://localhost/~cam/phylomatic-ws/eg/tol.nexml.xml \
	  #   -d method=convert -d informat=nexml \
	  #   http://localhost/~cam/phylomatic-ws/pmws > out

	  cmd = "curl --max-filesize 10000000 -L -s \"" f["intreeuri"] "\" > run/" PS ".nexml" ;
	  if (system(cmd) != 0)	{ error("copying file from url failed") }
	  close(cmd);
	  cmd2 = XGAWK " -f lib/nexml2fyt.awk run/" PS ".nexml";
	  RS = "\x04";
	  # getline results < "run/" PS ".fyt" ;
	  cmd2 | getline results ;
	  close(cmd2);
	  readfyt(results);
	}

  else if ((f["intree"]) &&  (f["informat"] == "cdaordf"))
	{
	  print f["intree"] > "run/" PS ".rdf" ;
	  close("run/" PS ".rdf"); # <- dont' forget this!
	  cmd = RAPPER " -i rdfxml -o ntriples run/" PS ".rdf";
	  RS = "\x04";
	  cmd | getline results ;
	  close(cmd);
	  cdao2fyt(results) ;
	}

  else if ((f["intreeuri"]) &&  (f["informat"] == "cdaordf"))
	{
	  cmd = "curl --max-filesize 10000000 -s -L -H \"Accept: application/rdf+xml\" \"" f["intreeuri"] "\" > run/" PS ".rdf" ;
	  if (system(cmd) != 0)	{ error("copying file from url failed") }
	  close(cmd);
	  cmd2 = RAPPER " -i rdfxml -o ntriples run/" PS ".rdf";
	  RS = "\x04";
	  cmd2 | getline results ;
	  close(cmd2);
	  cdao2fyt(results) ;
	}

  else if ((f["intree"]) &&  (f["informat"] == "newick"))
	{
	  new2fyt( f["intree"] );
	  #print "Content-type: text/plain\n\n";
	  #for (i in parent) print i, parent[i], bl[i], taxon[i] "\n";
	  #exit;
	}


  # METHODS:

  if (f["method"] == "phylomatic")
	{
	  # get taxa
	  print gensub(/\|/,"\n","G", encode(f["taxa"])) "\n" > "run/" PS ".taxa" ;

	  # tree data provided directly 
	  if (f["intree"]) 
		{
		  if ((f["informat"] == "newick") || (f["informat"] == ""))
			{
			  print encode(f["intree"]) > "run/" PS ".phylo" ;
			}
		  else if (f["informat"] == "nexml")
			{
			  print encode(f["intree"]) > "run/" PS ".nexml" ;
			  system("cat run/" PS ".nexml | ./nexml2newick > run/" PS ".phylo")			}
		}
	  else # intreeuri
		{
		}

	  # Run CLI phylomatic
	  system(PM " -t run/" PS ".taxa -f run/" PS ".phylo > run/" PS ".out") ;
	  RS = "\x04"; 
	  "cat run/" PS ".out" | getline results ;

	  print "Content-type: text/plain\n\n";
	  print decode(results);
	}

  # FORMAT CONVERSIONS
  else if (f["method"] == "convert")
	{
		  
	  print "Content-type: text/plain\n\n";
	  # print results;
	  fyt2new();

	}
  
}

